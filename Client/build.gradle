import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'org.greenrobot.greendao' // apply plugin
apply from: "${rootDir}/version.gradle"

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.example.client"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        // NDK编译多个平台
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }

        //APK打包多个平台
//        splits {
//            abi {
//                enable true
//                reset()
//                include 'x86'
//                universalApk true
//            }
//        }
    }

    signingConfigs {
        debug {
            storeFile file("${rootDir.absolutePath}/android.keystore")
            keyAlias "androidp"
            storePassword "androidp"
            keyPassword "androidp"
        }
        release {
            storeFile file("${rootDir.absolutePath}/android.keystore")
            keyAlias "androidp"
            storePassword "androidp"
            keyPassword "androidp"

        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            buildConfigField 'String','CUSTOMIZED_VERSION','"common"'
            signingConfig signingConfigs.debug
        }
        release {
            // 混淆设置
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            buildConfigField 'String','CUSTOMIZED_VERSION','"common"'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir('src/main/libs')
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

//    aaptOptions{
//        cruncherEnabled false
//    }

    greendao {
        schemaVersion 1                      // 指定数据库schema版本号，迁移等操作会用到;
        daoPackage 'com.example.client.dao'  // 生成DAOs、DaoMaster、DaoSession的包名。默认为entities所在包名。
        targetGenDir 'src/main/java'         // 生成DAOs、DaoMaster和DaoSession的目录, 默认为build/generated/source/greendao
    }

    lintOptions {
        abortOnError false
    }


    applicationVariants.all { variant ->
        // old gradle version
//        def output = variant.outputs[0]
//        File apk = output.outputFile
//        String Name = output.outputFile.name
//                .replace(".apk", "-${buildTime()}-${project.ext.versionInfo.build}.apk")
//        //output.outputFile = new File(apk.parentFile, Name)
//        outputFileName = Name
//        destinationDir = apk.parentFile
        variant.outputs.all { outputs ->
            outputFileName = "${project.name}-${buildTime()}-${project.ext.versionInfo.build}.apk"
        }
    }

//    project.tasks.whenTaskAdded { task ->
//        android.applicationVariants.all { variant ->
//            if (task.name == "prepare${variant.name.capitalize()}Dependencies") {
//                task.dependsOn ":library:assemble${variant.name.capitalize()}"
//            }
//        }
//    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains('AndroidTest')) {
        task.enabled = false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.3.1'
    compile project(':ScrollView')
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'org.greenrobot:greendao:3.2.2'
    compile 'org.greenrobot:eventbus:3.1.1'

    // RxJava
    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'

}

afterEvaluate {
    tasks.getByName("assembleDebug").doLast {
        copy {
            println buildDir
            println rootDir.absolutePath
            from("$buildDir/outputs/apk/debug/") {
                exclude '**/*unsigned.apk', '**/*unaligned.apk'
                include "**/${project.name}*.apk"
            }
            into "${rootDir.absolutePath}\\Client\\release"
            rename(/(.)*\.apk/, "${project.name}.apk")
        }
        println install()
        println date()
    }

    // 删除build文件夹
//    tasks.getByName("assembleRelease").doLast {
//        delete "${projectDir.absolutePath}/build"
//    }

}

static def buildTime() {
    return new Date().format("yyyy-MM-dd-HH-mm-ss", TimeZone.getTimeZone("GMT+8"))
}

def coreWrapperDir = new File("${projectDir}/src/main/java/com/example/client/jni")

task createCoreWrapperDir {
    coreWrapperDir.mkdirs()
}

// For this to work, it's assumed SWIG is installed
// FIXME: This only works when called from Command Line (gradlew swig)
task swig(type: Exec, dependsOn: ['createCoreWrapperDir']) {
    String osName = System.getProperty("os.name").toLowerCase();
    if (osName.contains("windows")) {
        commandLine 'cmd', '/c', 'swig'
        args '-java', '-package', 'com.example.client.jni', '-outdir', coreWrapperDir.absolutePath, '-o', "${projectDir}/src/main/jni/example_wrap.c", "${projectDir}/src/main/jni/Example.i"
    } else {
        commandLine 'swig'
        args '-c++', '-java', '-package', 'com.example.client.jni', '-outdir', coreWrapperDir.absolutePath, '-o', "${projectDir}/src/main/jni/example_wrap.c", "${projectDir}/src/main/jni/Example.i"
    }
}

// adb install [-r] [xxx.apk]
def install() {
    String cmd = "adb install -r ${rootDir.absolutePath}\\${project.name}\\release\\${project.name}.apk"
    cmd.execute().text.trim()
}

// adb push [xxx.apk] [destination]
task push(type: Exec, dependsOn: ['remount']) {
    commandLine 'cmd', '/c', 'adb', 'push', rootDir.absolutePath + "\\apk\\${project.name}.apk", 'system/app'
}

// adb remount
task remount(type: Exec) {
    commandLine 'cmd', '/c', 'adb', 'remount'
}

task deleteApk(type: Delete) {
    delete "${rootDir.absolutePath}/apk"
}

task makeJar(type: Jar, dependsOn: ['build']){
    delete 'libs/sdk.jar'
    baseName 'sdk'
    from('build/intermediates/classes/debug/com/example')
    into('com/example/')
    exclude('test/', 'BuildConfig.class', 'R.class')
    exclude{it.nama.startsWith('R$');}
}

task makeJar2(type: Jar, dependsOn: ['compileReleaseJavaWithJavac']){
    archiveName 'sdk.jar'
    from('build/intermediates/classes/release')
    from(project.zipTree("libs/universal-image-loader-1.9.5.jar"))
    destinationDir = file('build/libs')
    exclude('**/R.class')
    include('com/example/client/**')
    into('com/example/')
}

// adb install [-r] [xxx.apk]
def date() {
    File file = new File("${rootDir.absolutePath}\\${project.name}\\release\\${project.name}.apk")
    return "[${project.name}.apk] last modified time: " + new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(file.lastModified())
}

// call deleteApk when makeJar finished
makeJar.finalizedBy deleteApk

